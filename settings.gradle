pluginManagement {
    plugins {
        id "com.android.application" version "$androidToolsBuildGradleVer"
        id "com.android.library" version "$androidToolsBuildGradleVer"
        id "kotlin-android" version "$kotlinVer"
        id 'kotlin-parcelize' version "$kotlinVer" apply(false)
        id "kotlin-kapt" version "$kotlinVer"
    }
}

/**
 * Добавляем в проект указанный модуль и его api и impl модули сабмодули, при их наличии.
 */
def includeModules(String modulePath) {
    def dir = modulePath.replaceAll(":", "/").substring(1)
    def dirFile = file(dir)
    include(modulePath)
    dirFile.eachDir { subDir ->
        def subDirName = subDir.name
        if (subDirName == "api" || subDirName == "impl") include(modulePath + ":" + subDir.name)
    }
}

include ':modules'
includeModules(":modules:app")


include ':modules:domain'
includeModules(":modules:domain:core")
includeModules(":modules:domain:network")
includeModules(":modules:domain:security")
includeModules(":modules:domain:cart")


include ':modules:feature'
includeModules(":modules:feature:core")
includeModules(":modules:feature:root")
includeModules(":modules:feature:employeeAuth")
includeModules(":modules:feature:main")
includeModules(":modules:feature:cart")
includeModules(":modules:feature:catalogue")
includeModules(":modules:feature:pdp")
