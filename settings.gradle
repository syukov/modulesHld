pluginManagement {
    // see plugins version declaration in gradle.properties
    plugins {
        id "com.android.application" version "$androidToolsBuildGradleVer"
        id "com.android.library" version "$androidToolsBuildGradleVer"
        id "kotlin-android" version "$kotlinVer"
        id 'kotlin-parcelize' version "$kotlinVer" apply(false)
        id "kotlin-kapt" version "$kotlinVer"
    }
}

/**
 * Добавляет в проект модуль лежащий по указанному пути + модули из всех поддиректорий первого уровня указанного пути.
 * Используется для добавления api и impl моделей одной строкой.
 */
def includeModules(String dir) {
    def dirFile = file(dir)
    if (!dirFile.exists()) return
    def modulePath = ":" + dir.replaceAll("/", ":")
    include(modulePath)
    dirFile.eachDir { subDir ->
        include(modulePath + ":" + subDir.name)
    }
}

include ':modules'
includeModules("modules/app")

include ':modules:domain'
includeModules("modules/domain/core")
includeModules("modules/domain/network")
includeModules("modules/domain/security")
includeModules("modules/domain/cart")

// feature:

include ':modules:feature'
includeModules("modules/feature/core")
includeModules("modules/feature/cart")
includeModules("modules/feature/catalogue")
includeModules("modules/feature/employeeAuth")
includeModules("modules/feature/pdp")

include ':root-feature'
project(':root-feature').projectDir = new File(rootDir, 'modules/feature/root-feature')
include ':root-feature-api'
project(':root-feature-api').projectDir = new File(rootDir, 'modules/feature/root-feature-api')

include ':main-feature'
project(':main-feature').projectDir = new File(rootDir, 'modules/feature/main-feature')
include ':main-feature-api'
project(':main-feature-api').projectDir = new File(rootDir, 'modules/feature/main-feature-api')